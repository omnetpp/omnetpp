%description:
Test NED exists() operator that tells whether a submodule exists or not

%file: test.ned
import testlib.Dump;

module Node { gates: output o[]; input i[]; connections allowunconnected: }
module Params { parameters: bool existentExists; bool nonexistentExists; }

network Test
{
    parameters:
        // test exists() in param assignment (should cause error, but for now it just returns false)
        bool existentExists = exists(existent);
        bool nonexistentExists = exists(nonexistent);

    submodules:
        existent: Node if true;
        nonexistent: Node if false;

        // test exists() in submodule condition
        ifExistent: Node if exists(existent);
        ifNonexistent: Node if exists(nonexistent);

        // test exists() in submodule param assignment
        params: Params {
            existentExists = exists(parent.existent);
            nonexistentExists = exists(parent.nonexistent);
        }

        a: Node;
        b: Node;
        dump: Dump;

    connections allowunconnected:
       // test exists() in connection "if" condition
       a.o++ --> { delay = 1s; } --> b.i++ if exists(existent);
       a.o++ --> { delay = 2s; } --> b.i++ if exists(nonexistent);

       // test exists() in channel param assignment
       a.o++ --> { delay = exists(parent.existent) ? 10s : 0s; } --> b.i++;
       a.o++ --> { delay = exists(parent.nonexistent) ? 0s : 20s; } --> b.i++;
}

%contains: stdout
module Test: Test {
    parameters:
        @isNetwork
        existentExists = false
        nonexistentExists = false
    submodules:
        module Test.existent: Node {
        }
        module Test.ifExistent: Node {
        }
        module Test.params: Params {
            parameters:
                existentExists = true
                nonexistentExists = false
        }
        module Test.a: Node {
            gates:
                o[0]: --> b.i[0], (ned.DelayChannel)channel disabled=false delay=1s
                o[1]: --> b.i[1], (ned.DelayChannel)channel disabled=false delay=10s
                o[2]: --> b.i[2], (ned.DelayChannel)channel disabled=false delay=20s
        }
        module Test.b: Node {
            gates:
                i[0]: <-- a.o[0], (ned.DelayChannel)channel disabled=false delay=1s
                i[1]: <-- a.o[1], (ned.DelayChannel)channel disabled=false delay=10s
                i[2]: <-- a.o[2], (ned.DelayChannel)channel disabled=false delay=20s
        }
}
