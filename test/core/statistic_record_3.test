%description:
Tests the **.statistic-recording= configuration option.

%file: test.ned

simple Node extends testlib.StatNode
{
    @statistic[dorecord](source=foo; record=last,min,max);
    @statistic[norecord](source=foo; record=last,min,max);
    @statistic[dummy](source=foo; record=last);  // to add a delimiter line at the end of the sca file
}

network Test
{
    submodules:
        node: Node;
}

%inifile: omnetpp.ini
debug-statistics-recording = true

**.dorecord.statistic-recording = true
**.dummy.statistic-recording = true
**.statistic-recording = false

**.dorecord:min.scalar-recording = false

%subst: /signalID=\d+/signalID=_/

%contains: stdout
Test.node (Node):
    "foo" (signalID=_):
        omnetpp::LastValueRecorder ==> dorecord:last
        omnetpp::MinRecorder ==> dorecord:min
        omnetpp::MaxRecorder ==> dorecord:max
        omnetpp::LastValueRecorder ==> dummy:last

%contains: results/General-#0.sca
scalar Test.node dorecord:last 17
attr recordingmode last
attr source foo
scalar Test.node dorecord:max 28
attr recordingmode max
attr source foo
scalar Test.node dummy:last 17
attr recordingmode last
attr source foo

